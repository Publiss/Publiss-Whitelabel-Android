apply plugin: 'com.android.application'
apply from: '../gradle/quality.gradle'

android {
    compileSdkVersion 20
    buildToolsVersion "20.0.0"

    signingConfigs {
        publiss {
            def keyStoreFile = System.getenv("ANDROID_PUBLISS_KEYSTORE_PATH")

            if (keyStoreFile == null) {
                keyStoreFile = "/dummy.pks"
            }
            // We can leave these in environment variables
            storeFile file(keyStoreFile)
            keyAlias System.getenv("ANDROID_PUBLISS_KEY_ALIAS")

            // These two lines make gradle believe that the signingConfigs
            // section is complete. Without them, tasks like installRelease
            // will not be available!
            storePassword System.getenv("ANDROID_PUBLISS_KEYSTORE_PASSWORD")
            keyPassword System.getenv("ANDROID_PUBLISS_KEY_PASSWORD")
        }
    }

    defaultConfig {
        applicationId PACKAGE_PREFIX + ".publissplaystore"
        minSdkVersion 15
        targetSdkVersion 20
        versionCode retrieveVersionCode()
        println "VersionCode: " + versionCode
        versionName generateVersionName(versionCode)
        println "VersionName: " + versionName
    }

    buildTypes {
        release {
            zipAlign true
            runProguard false
            signingConfig signingConfigs.publiss
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                def file = variant.outputFile
                def manifestParser = new com.android.builder.core.DefaultManifestParser()
                variant.outputFile = new File(file.parent, file.name.replace("app-", "publiss-" + System.getenv("ANDROID_APK_TYPE") + "-"))
            }
        }
    }

    android.applicationVariants.all{ variant ->
        variant.mergeResources.doLast{
            ext.env = System.getenv()
            File valuesFile = file("${buildDir}/intermediates/res/${variant.dirName}/values/values.xml")
            println("Replacing content provider authority in " + valuesFile)
            println("Content provider authority = " + PACKAGE_PREFIX)
            String content = valuesFile.getText('UTF-8')
            content = content.replaceAll(/content_provider_authority_placeholder/, PACKAGE_PREFIX)
            valuesFile.write(content, 'UTF-8')
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        lintConfig file("lint.xml")
    }
}

dependencies {
    compile project(':publiss-android-core')
}

def retrieveVersionCode() {
    def calculatedVersionCode = System.getenv("BUILD_NUMBER");
    if (null == calculatedVersionCode) {
        calculatedVersionCode = "-1";
    }
    return Integer.parseInt(calculatedVersionCode)
}

def getGitDescribe() {
    try {
        return 'git describe --tags'.execute().text.trim()
    } catch (IOException exception) {
        throw new UnsupportedOperationException("Could not find git! Maybe it is not in \$PATH variable?", exception)
    }
}

def getCurrentGitBranch() {
    try {
        return 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()
    } catch (IOException exception) {
        throw new UnsupportedOperationException("Could not find git! Maybe it is not in \$PATH variable?", exception)
    }
}

def generateVersionName(buildNumber) {
    def versionName
    if (buildNumber == -1) {
        // local build
        versionName = getGitDescribe() + " " + getCurrentGitBranch()
    } else {
        // jenkins build
        versionName = getGitDescribe()
    }
    return versionName
}

def overwritePackageName() {
    try {
        /*commands = 'FILE="$WORKSPACE/app/src/main/res/values/publiss_config.xml"\n' +
                   'PID=$' + packageName + '\n' +
                   'sed -i \'\' "s/\$' + placeholder + '/$PID/g" $FILE\n'*/
        'A_FILE="A_File"'.execute()
    } catch (IOException exception) {
        throw new UnsupportedOperationException("Overwriting package name in strings failed", exception)
    }
}

